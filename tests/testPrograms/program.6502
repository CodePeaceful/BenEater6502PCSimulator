PORTB = $6000
PORTA = $6001
DDRB = $6002
DDRA = $6003

value = $0200   ; 2 bytes
mod10 = $0202   ; 2 bytes
message = $0204 ; 6 bytes
counter = $020a ; 2 bytes

E   = %10000000
RW  = %01000000
RS  = %00100000

    .org $8000

reset:          ; prints the number of interupts on the lcd
    ldx #$ff
    txs
    cli
    
    lda #%11111111  ; Set all pins on PORT B to output
    sta DDRB

    lda #%11100000  ; Set top 3 pins on PORT A to output
    sta DDRA

    lda #%00111000  ; Set 8-bit mode; 2-line display; 5x8 font
    jsr lcd_instuction
    lda #%00001100  ; display on; cursor off; blink off
    jsr lcd_instuction
    lda #%00000110  ; increment and shift curser; not shifting display
    jsr lcd_instuction
    lda #%00000001  ; Clear display
    jsr lcd_instuction

    stz counter
    stz counter + 1

loop:
    lda #%00000010 ; Home
    jsr lcd_instuction


    stz message

    ; initialize value to be the number to convert
    lda counter
    sta value
    lda counter + 1
    sta value + 1

divide:
    ; initialize the reminder to zero
    stz mod10
    stz mod10 + 1
    clc

    ldx #16
divloop:
    ; rotate quotient and reminder
    rol value
    rol value + 1
    rol mod10
    rol mod10 + 1

    ; a,y = dividend - divisor
    sec
    lda mod10
    sbc #10
    tay     ; save low byte in Y
    lda mod10 + 1
    sbc #0
    bcc ignore_result ; brach if dividend < divisor
    sty mod10
    sta mod10 + 1

ignore_result:
    dex
    bne divloop
    rol value   ; shift in the last bit of the quotient
    rol value + 1

    lda mod10
    clc
    adc #"0"
    jsr push_char

    ; if value != 0, then continue dividing
    lda value
    ora value + 1
    bne divide  ; brach if value not zero

    ldx #0
print:
    lda message,x
    beq loop
    jsr print_char
    inx
    jmp print

    jmp loop


; Add the character in the A register to the beginning of the
; null-terminated string 'message'
push_char:
    pha
    ldy #0

char_loop:
    lda message,y
    tax
    pla
    sta message,y
    iny
    txa
    pha
    bne char_loop

    pla
    sta message,y

    rts


lcd_instuction:
    jsr lcd_wait
    sta PORTB  
    stz PORTA       ; Clear RS/RW/E
    lda #E          ; Set enable bit to send instruction
    sta PORTA
    stz PORTA       ; Clear RS/RW/E
    rts

print_char:
    jsr lcd_wait
    sta PORTB
    lda #RS          ; Clear RW/E
    sta PORTA
    lda #(RS | E)    ; Set enable bit to send instruction
    sta PORTA
    lda #RS          ; Clear RW/E
    sta PORTA
    rts

lcd_wait:
    pha
    stz DDRB
lcd_wait_loop:
    lda #RW
    sta PORTA
    lda #(RW | E)
    sta PORTA
    lda PORTB
    and #%10000000
    bne lcd_wait_loop
    lda #%11111111
    sta DDRB
    pla
    rts

nmi:
irq:
    inc counter
    bne exit_irq
    inc counter + 1
exit_irq:
    rti

    .org $fffa
    .word nmi
    .word reset
    .word irq